#!/usr/bin/env ucblogo

MAKE "noise (RANDOM 2147483646) ; (LAST LAST SHELL [date +%s])

TO hTab
  OUTPUT CHAR 9
END

TO vTab :qp
  REPEAT :qp [
    PRINT CHAR 11
  ]
END

TO sHead :nbr :qp
  TEST :nbr >= COUNT :qp
  ifTRUE  [
    OUTPUT :qp
  ]
  ifFALSE [
    OUTPUT sHead :nbr BL :qp
  ]
END

TO sTail :nbr :qp
  TEST :nbr >= COUNT :qp
  ifTRUE  [
    OUTPUT :qp
  ]
  ifFALSE [
   OUTPUT sTail :nbr BF :qp
  ]
END

TO strBj :qp
  OUTPUT WORD (sTail 10 :qp) (sHead 50 :qp)
END

TO strFn :qp
  OUTPUT WORD (sTail 35 :qp) (sHead 25 :qp)
END

TO strCn :qp
  OUTPUT WORD (sTail  0 :qp) (sHead 60 :qp)
END

TO strGn :qp
  OUTPUT WORD (sTail 25 :qp) (sHead 35 :qp)
END

TO strDn :qp
  OUTPUT WORD (sTail 50 :qp) (sHead 10 :qp)
END

TO strAn :qp
  OUTPUT WORD (sTail 15 :qp) (sHead 45 :qp)
END

TO strEn :qp
  OUTPUT WORD (sTail 40 :qp) (sHead 20 :qp)
END

TO strBn :qp
  OUTPUT WORD (sTail  5 :qp) (sHead 55 :qp)
END

TO strFk :qp
  OUTPUT WORD (sTail 30 :qp) (sHead 30 :qp)
END

TO augmenTriad :qp
  PR  WORD hTab strDn :qp
  PR  WORD hTab strBj :qp
  PR  WORD hTab strFk :qp
END

TO maj3rds :key :qp
  PR (WORD hTab :key "-fkbjdn-sv noise)
  REPEAT 2 [
    augmenTriad :qp
  ]
END

TO stdGtr :key :qp
  PR (WORD hTab :key "-eadgbe-sv noise)
  PR  WORD hTab strEn :qp
  PR  WORD hTab strBn :qp
  PR  WORD hTab strGn :qp
  PR  WORD hTab strDn :qp
  PR  WORD hTab strAn :qp
  PR  WORD hTab strEn :qp
END

TO all4ths :key :qp
  PR (WORD hTab :key "-beadgcf-sv noise)
  PR  WORD hTab strFn :qp
  PR  WORD hTab strCn :qp
  PR  WORD hTab strGn :qp
  PR  WORD hTab strDn :qp
  PR  WORD hTab strAn :qp
  PR  WORD hTab strEn :qp
  PR  WORD hTab strBn :qp
END

TO cello5ths :key :qp
  PR (WORD hTab :key "-cgdae-sv noise)
  PR  WORD hTab strEn :qp
  PR  WORD hTab strAn :qp
  PR  WORD hTab strDn :qp
  PR  WORD hTab strGn :qp
  PR  WORD hTab strCn :qp
END

TO triTones :key :qp
  PR (WORD hTab :key "-bfbf-sv noise)
  PR  WORD hTab strFn :qp
  PR  WORD hTab strBn :qp
  PR  WORD hTab strFn :qp
  PR  WORD hTab strBn :qp
END

TO displayMenu
  CLEARTEXT
  vTab 1
  PR "|	n0 	k6 	j17 	k6x5 	j17y2 	j3     |
  PR "|	j34k6 	j17k2 	n26y5 	k26x5 	j6 	j36    |
  PR "|	k56 	j136y7 	k56x4 	n167x4 	j3k5x4 	j167y2 |
  PR "|	j2k56x4 k157x6 	k1j6 	n345 	j2 	j26    |
  PR "|	j236 	j23 	j23k6 	j2y3 	j2k6 	j26y3  |
  PR "|	j2k56 	j246y3 	j26y34 	j2k6x5 	j2k6y3 	j3k6   |
  PR "|	n45y2 	j3k56x4 k2j6 	n5y2 	k26 	k256   |
  PR "
  PR "|	n0 	j5 	k34 	j5y6 	k34x2 	k1     |
  PR "|	k17j5 	j2k34 	n25x6 	j25y6 	k5 	k15    |
  PR "|	j56 	k135x4 	j56y7 	n345y7 	k1j6y7 	k345x2 |
  PR "|	k2j56y7 j346y5 	j3k5 	n167 	k2 	k25    |
  PR "|	k125 	k12 	k12j5 	k2x1 	k2j5 	k25x1  |
  PR "|	k2j56 	k257x1 	k25x17 	k2j5y6 	k2j5x1 	k1j5   |
  PR "|	n67x2 	k1j56y7 j2k5 	n6x2 	j25 	j256   |
  vTab 1
END

TO Plano.Graphic :qp
  CLEARTEXT
  IF (EQUALP :qp []) [
    LOCALMAKE "qp [n0 n0]
  ]
  IF AND (MEMBERP FIRST :qp PLIST "ZG) (MEMBERP LAST :qp PLIST "ZG) [
    vTab 1
      stdGtr (FIRST :qp) (GPROP "ZG (FIRST :qp))
    vTab 1
      stdGtr ( LAST :qp) (GPROP "ZG ( LAST :qp))
    vTab 1
  ]
  IF OR (NOT MEMBERP FIRST :qp PLIST "ZG) (NOT MEMBERP LAST :qp PLIST "ZG)[
    vTab 2
      PR SE hTab [Check your selection!]
    vTab 1
      PR (SE hTab "Example: [k56x4 j56y7])
    vTab 1
      WAIT 120
      PR (SE hTab [Focus and try again!])
    vTab 4
      WAIT 120
      CLEARTEXT
    Litho.Graphic
  ]
END

TO Litho.Graphic
  displayMenu
  PR [Enter two (2) signatures from above ^]
  Plano.Graphic READLIST
END

